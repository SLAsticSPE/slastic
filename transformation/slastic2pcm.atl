-- @path Slastic=/Slastic2Pcm/model/slastic.ecore
-- @path Pcm=/Slastic2Pcm/model/pcm.ecore

module slastic2pcm;
create OUT: Pcm from IN: Slastic;

--------------------------------------------------------------------------------
-- Transform typeRepositoryModel to RepositoryModel
--------------------------------------------------------------------------------

rule NewInterface(componentType: Slastic!ComponentType) {
	to tgt: Pcm!Interface (
		id <- 'i' + componentType.id.toString(),
		entityName <- 'I' + componentType.name,
		signatures__Interface <- componentType.operations->collect(operation | 
			operation.signature
		)
	) 
	do {
		tgt;
	}
}

rule NewProvidedRole(interface: Pcm!Interface, componentType: Slastic!ComponentType) {
	to tgt: Pcm!ProvidedRole (
		entityName <- 'Provided_' + interface.entityName + '_' + componentType.name,
		providedInterface__ProvidedRole <- interface
	)
	do {
		tgt;
	}
}

rule NewParameter(name: String) {
	to tgt: Pcm!Parameter (
		parameterName <- name
	) 
	do {
		tgt;
	}
}

rule TypeRepositoryModelToRepository {
	from src: Slastic!TypeRepositoryModel
	to tgt: Pcm!Repository (
		-- Create dummy interfaces.
		interfaces__Repository <- src.componentTypes->collect(component | 
			thisModule.NewInterface(component)
		),
		components__Repository <- src.componentTypes
	)
}

rule ComponentTypeToBasicComponent {
	from src: Slastic!ComponentType
	to tgt: Pcm!BasicComponent (
		id <- src.id.toString(),
		entityName <- src.name,
		providedRoles_InterfaceProvidingEntity <- thisModule.NewProvidedRole(
			tgt.repository_ProvidesComponentType.interfaces__Repository->any(interface | 
				interface.id = 'i' + src.id.toString() 
			),
			src
		)
	)
}

rule SignatureToSignature {
	from src: Slastic!Signature
	to tgt: Pcm!Signature (
		serviceName <- src.name,
		parameters__Signature <- src.paramTypes->collect(paramType | 
			thisModule.NewParameter(paramType)
		)
	)
}

--------------------------------------------------------------------------------
-- Transform componentAssemblyModel to SystemModel
--------------------------------------------------------------------------------

rule ComponentAssemblyModelToSystem {
	from src: Slastic!ComponentAssemblyModel
	to tgt: Pcm!System (
		childComponentContexts_ComposedStructure <- src.assemblyComponents,
		compositeAssemblyConnectors_ComposedStructure <- src.assemblyConnectors --?
	)
}

rule AssemblyComponentToAssemblyContext {
	from src: Slastic!AssemblyComponent
	to tgt: Pcm!AssemblyContext (
		entityName <- 'Assembly_' + src.name,
		encapsulatedComponent_ChildComponentContext <- src.componentType
	)
}

--------------------------------------------------------------------------------
-- Transform componentDeploymentModel to AllocationModel
--------------------------------------------------------------------------------

rule ComponentDeploymentModelToAllocation {
	from src: Slastic!ComponentDeploymentModel
	to tgt: Pcm!Allocation (
		
	)
}

--------------------------------------------------------------------------------
-- Transform executionEnvironmentModel to ResourceEnvironmentModel
--------------------------------------------------------------------------------

rule InitializeResourceRepository() {
	to tgt: Pcm!ResourceRepository(
		availableResourceTypes_ResourceRepository <- Sequence{
			thisModule.NewProcessingResourceType('CPU','_oro4gG3fEdy4YaaT-RYrLQ'),
			thisModule.NewProcessingResourceType('HDD','_BIjHoQ3KEdyouMqirZIhzQ'),
			thisModule.NewProcessingResourceType('DELAY', '_nvHX4KkREdyEA_b89s7q9w'),
			thisModule.NewCommunicationLinkResourceType('LAN', '_o3sScH2AEdyH8uerKnHYug')
		}
	)
}

rule NewProcessingResourceType(name: String, id: String){
	to tgt: Pcm!ProcessingResourceType(
		entityName <- name,
		id <- id
	) do { tgt; }
}

rule NewCommunicationLinkResourceType(name: String, id: String){
	to tgt: Pcm!CommunicationLinkResourceType(
		entityName <- name,
		id <- id
	) do { tgt; }
}

rule ExecutionEnvironmentModelToResourceEnvironment {
	from src: Slastic!ExecutionEnvironmentModel
	to tgt: Pcm!ResourceEnvironment (
		resourceContainer_ResourceEnvironment <- src.executionContainers
	)
	do {
		-- TODO Resource repository should be dynamically loaded, instead of being created with fixed parameters.
		thisModule.InitializeResourceRepository();
	}
}

rule ExecutionContainerToResourceContainer {
	from src: Slastic!ExecutionContainer
	to tgt: Pcm!ResourceContainer (
		id <- src.id.toString(),
		entityName <- src.name,
		activeResourceSpecifications_ResourceContainer <- src.executionContainerType.resources
	)
}

rule ResourceSpecificationToProcessingResourceSpecification {
	from src: Slastic!ResourceSpecification
	to tgt: Pcm!ProcessingResourceSpecification (
		-- TODO Support resource types other than cpu resources.
		activeResourceType_ActiveResourceSpecification <- 
		if src.resourceType.name = 'CPU_RESOURCE_TYPE'
			then Pcm!ProcessingResourceType.allInstances()->any(type | type.id = '_oro4gG3fEdy4YaaT-RYrLQ')
			else OclUndefined
		endif
	)
}
